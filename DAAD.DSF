;------------------------------------------------------------------------------------------
;ÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑ
;ÑÑÑÑÑ                                                                                  ÑÑÑÑÑ
;ÑÑÑÑÑ Este es el fichero de inicio de la librería para DAAD Reborn Compiler en español ÑÑÑÑÑ
;ÑÑÑÑÑ                                                                                  ÑÑÑÑÑ
;ÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑ

; Por favor no quites las eñes en el recuadro justo encima, están ahí para que notes fácil-
; mente si abres el fichero con una codificación que no es la correcta (ISO-8859-1 o Win1252)

/CTL    ;Sección de control
_
;#extern "MLV_MSX.BIN"
;#classic
;#debug

#define TIMEOUT   87            ;If Timeout last frame

; System flags 0-63
#define fDark               0
#define fObjectsCarried     1

#define fDarkF              28
#define fGFlags             29     ; This is best tested using HASAT GMODE
#define fScore              30
#define fTurns              31    ; 2 bytes
#define fTurnsHi            32

#define fVerb               33
#define fNoun               34
#define fAdject1            35
#define fAdverb             36
#define fMaxCarr            37
#define fPlayer             38
#define fPrep               43
#define fNoun2              44
#define fAdject2            45
#define fCPronounNoun       46
#define fCPronounAdject     47
#define fTimeout            48
#define fTimeoutFlags       49
#define fDoallObjNo         50
#define fRefObject          51
#define fStrength           52
#define fObjFlags           53
#define fRefObjLoc          54
#define fRefObjWeight       55
#define fRefObjIsContainer  56
#define fRefObjisWearable   57
#define fRefObjAttr1        58
#define fRefObjAttr2        59
#define fInkeyKey1          60
#define fInkeyKey2          61
#define fScreenMode         62     ; 2=Text, 4=CGA, 13=EGA, 141=VGA
#define fCurrentWindow      63     ; Which window is active at the moment

;Flags de usuario   
#define fPuertaLenera       64
#define fPuertaGarage       65
#define fPuertaPorche       66
#define fUsarMovil          67
#define fNoUsarMovil        68
#define fExaminarMaceta     69
#define fCogerFelpudo       70
#define fCogerGnomo         71
#define fCerillas           72
#define fLuz                73
#define fLuz2               74
#define fCajon              75
#define fMesita             76
#define fTablon             77
#define fUsarInterruptor    78


;localidades
#define lIntro       0
#define lMadrid      1
#define lCoche       2
#define lExtcasa     3
#define lLenera      4
#define lGarage      5
#define lPorche      6
#define lDescansillo 7
#define lBodega      8
#define lAseo        9
#define lCocina      10
#define lSalon       11
#define lEscaleras   12
#define lAlmacen     13
#define lPatio       14
#define lSecret2     15
#define lPasillo1    16
#define lPasillo2    17
#define lPasillo3    18

;Objetos
#define oLinterna       0
#define oLlave1         1
#define oLlave2         2
#define oBateriaCoche   3
#define oPinzasCoche    4
#define oLlavePorche    5
#define oFelpudo        6
#define oMaceta         7
#define oGnomo          8
#define oCerillas       9
#define oTablon         10
#define oSecretKey1     11
#define oLlaveCaja      12
#define oMovil          13
#define oMechero        14
#define oLlavePatio     15
#define oFotografia1    16



;------------------------------------------------------------------------------
/VOC    ;Vocabulario
;                       Movimiento: verbos y nombres < 14
N       2       noun
NORTE   2       noun
S       3       noun
SUR     3       noun
E       4       noun
ESTE    4       noun
O       5       noun
OESTE   5       noun
NE      6       noun
NO      7       noun
SE      8       noun
SO      9       noun
AR      10      noun
ARRIBA  10      noun
SUBIR   10      verb
AB      11      noun
ABAJO   11      noun
BAJAR   11      verb
SALID   12      verb

;                       Nombres <20 indica que se pueden usar como verbos
;                               <50 indica nombre propio (no admite lo, la...)
I       14      noun
INVEN   14      noun
TODO    20      noun
TODOS   20      noun
TODAS   20      noun

; Objetos
MESITA   99      noun
MOVIL    100     noun
LLAV1    101     noun
LLAV2    102     noun
BATERIA  103     noun
PINZAS   104     noun
LLAV0    105     noun
FELPUDO  106     noun
MACETA   107     noun
GNOMO    108     noun
PORCHE   109     noun
METALICA 110     noun
PUERTA   111     noun
CAJON    112     noun
CERILLAS 113     noun
BODEGA   114     noun
ESCALERAS 115    noun
LLAVE     116    noun
TABLON    117    noun
CAJA      118    noun
TARJETA   119    noun
LLAV3     120    noun
SKEY1     121    noun
KCAJA     122    noun
LINTERNA  123    noun
MUEBLE    124    noun
FREGADERO 125    noun
LAVADORA  126    noun
TELEFONO  127    noun
MECHERO   128    noun
INTERRUPTOR 129  noun
ASEO      130    noun
SUMIDERO  131    noun
PLANTA    132    noun
FOTOGRAFIA     133    noun

;                       Verbos
COGER   20      verb
DEJAR   21      verb
SOLTAR  21      verb
QUITAR  22      verb
PONER   23      verb
R       24      verb
M       24      verb
MIRAR   24      verb
REDES   24      verb
F       25      verb
FIN     25      verb
SAVE    26      verb
SV      26      verb
LOAD    27      verb
LD      27      Verb
RAMSA   28      verb
RAMLO   29      verb
EX      30      verb
EXAMI   30      verb
DECIR   31      verb
PREGUNT 31      verb
HABLAR  31      verb
JUGAR   32      verb

;verbos jugador
USAR     33      verb
DORMIR   34      verb
TIRAR    35      verb
ESCUCHAR 36      verb
ENCENDER 37      verb
ABRIR    38      verb
PRENDER  39      verb
MOVER    40      verb
GOLPEAR  41      verb
LANZAR   42      verb
ROMPER   43      verb
X        44      verb

;                            Adjetivos
PEQUE   2       adjective
GRANDE  3       adjective
VIEJO   4       adjective
VIEJA   4       adjective
NUEVO   5       adjective
NUEVA   5       adjective
DURO    6       adjective
DURA    6       adjective
BLANDO  7       adjective
CORTO   8       adjective
CORTA   8       adjective
LARGO   9       adjective
LARGA   9       adjective
;                               Adverbios
RAPID   2       adverb
DESPA   3       adverb
LENTA   3       adverb
CUIDAd  5       adverb
;                               Prepositions
A       2       preposition
DE      3       preposition
EN      4       preposition
POR     5       preposition
TRAVES  5       preposition
BAJO    7       preposition
JUNTO   8       preposition
MENOS   13      preposition
;                               Pronombres
;                               Conjunciones
Y       2       conjugation
ENTONCE 2       conjugation
LUEGO   2       conjugation


;------------------------------------------------------------------------------
/STX    ;Mensajes del sistema
/0 "Está demasiado oscuro para ver nada."
/1 "También puedes ver:"
/2 "#n"
/3 "#n"
/4 "#n"
/5 "#n"
/6 "No he entendido nada."
/7 "No puedes ir en esa dirección."
/8 "No puedes hacer eso."
/9 "Llevo:#n"
/10 "Llevas puesto:#n"
/11 "";*Libre
/12 "¿Seguro?"
/13 "¿Quieres intentarlo de nuevo?"
/14 ""  ;*Libre
/15 "Vale."
/16 "Pulsa una tecla para continuar."
/17 "" ;*Has jugado
/18 "" ;*\sturno
/19 "" ;*s
/20 "" ; *.
/21 "" ; *Ha resuelto un
/22 "" ; *%
/23 "No llevas eso puesto."
/24 "No puedes. Tienes puesto _."
/25 "Ya tienes _."
/26 "Aquí no hay nada de eso."
/27 "No puedes llevar nada más."
/28 "No tienes eso.#n"
/29 "Ya llevas puesto _.#n"
/30 "S";Un solo carácter en mayúsculas. Inicial de Sí
/31 "N"  ;Un solo carácter en mayúsculas. Inicial de No
/32 "Hay más..."
/33 ">"
/34 "" ;*Libre
/35 "#nEl tiempo pasa..."
/36 "Has cogido _."
/37 "Te has puesto _."
/38 "Te has quitado _."
/39 "Has dejado _."
/40 "No puedes ponerte _."
/41 "No puedes quitarte _."
/42 "No puedes quitarte _.  No puedo llevar nada más en las manos."
/43 "@ pesa demasiado para tí."
/44 "@ está en"
/45 "@ no está en"
/46 ", "
/47 " y "
/48 ".#n"
/49 "No tienes _."
/50 "No llevas puesto _."
/51 ".#n"
/52 "No hay nada de eso en"
/53 "Nada.#n"
/54 "C" ; Inicial de Cinta
/55 "D" ; Disco
/56 "Unidad no preparada. Pulsa una tecla para volver a intentarlo."
/57 "Error de entrada/salida."
/58 "El disco o el directorio puede estar lleno."
/59 "Nombre de fichero no válido."
/60 "Nombre del fichero:"
/61 "Pon en marcha la cinta."
/62 "¿Cinta o disco?"
/63 "Salidas: "
/64 "N," 
/65 "S," 
/66 "E," 
/67 "O,"

;------------------------------------------------------------------------------
/MTX    ;Mensajes
/0 "Salidas visibles: "
/1 "ninguna"
/2 "norte "
/3 "sur "
/4 "este "
/5 "oeste "
/6 "noreste "
/7 "noroeste "
/8 "sureste "
/9 "suroeste "
/10 "arriba "
/11 "abajo "
/12 "entrar "
/13 "salir "

;------------------------------------------------------------------------------
/OTX    ;Object Texts
/0 "una linterna"
/1 "la llave de la leñera"
/2 "la llave del garage"
/3 "una batería de coche"
/4 "unas pinzas de coche"
/5 "una llave de la entrada principal"
/6 "un felpudo"
/7 "una maceta"
/8 "un Gnomo"
/9 "una caja de cerillas"
/10 "una tarjeta de seguridad"
/11 "una llave dorada"
/12 "la llave de una caja pequeña"
/13 "un teléfono móvil"
/14 "un mechero"
/15 "la llave del patio"
/16 "una fotografía"

;------------------------------------------------------------------------------
/LTX    ;Location Texts
/0 "Marcos es un tipo serio, con carácter, muy trabajador y responsable. Sus amigos lo ven como un soltero amargado, que no echa un polvo desde ni se sabe y por eso tiene ese mal genio.#n#n Está de fin de semana, en casa, tranquilo. No tiene planes excepto disfrutar de las pequeñas cosas de sus 48 horas de libertad condicional. Así denomina el, los fines de semana.#n#n Es sábado, son las 08:00AM de un fresquito día de invierno y aún sigues en la cama. Semi despierto y sin ganas de levantarte. Madrugas mucho durante la semana y no te apetece mucho abandonar el calorcito que sientes bajo el plumón.#n#n"
/1 "Te giras en la cama con la firme intención de dormir un poquito más pero, suena el teléfono móvil. Alguien ya decidió por ti que te debes despertar del todo. Tienes a tu lado la mesita de noche."
/2 "Coges tu coche rumbo a la sierra de gredos desde Madrid. Es un paseito, pero el viaje se hace ameno escuchando un poco de música. Te desvías hacia una carretera comarcal. Al final de la misma, hay una salida a la derecha que lleva directamente a la cabaña de tus padres. El camino tiene una preciosa arboleda a los lados de modo que hace un efecto tunel que siempre te ha dado respeto cuando eras un niño. Cuando se levantaba niebla, tu imaginación echaba a volar."
/3 "Estás en el exterior de la cabaña. Es prácticamente de noche. La verdad es que tus padres hicieron una preciosa reforma a la misma. Tiene dos plantas, y desde aquí, puedes ver al Norte la leñera, al Sur el garage y al Este tienes el porche, es decir, la entrada principal."
/4 "Estás en la leñera. Puedes Entrar o volver al Sur"
/5 "Lograste entrar en el garage. Que fácil es todo cuando tienes las llaves"
/6 "Un par de potentes luces iluminan el porche. Nunca te gustaron esas luces porque atraían muchos insectos las noches de verano y era imposible estar allí sin sufrir el feroz ataque de un mosquito sediento de sangre. Frente a ti está la puerta principal de la cabaña."
/7 "Estás en el descansillo."
/8 "Estás en la bodega."
/9 "Estás en el aseo. No hay apenas luz y no logras ver nada. Tienes un interruptor a la derecha. Quizás, si lo usas para encender la luz..."
/10 "Estás en la cocina. Es una preciosidad y además es enorme. Tiene un toque vintage muy chulo, todo de madera. Puedes ver el fregadero, algunos muebles, un almacén para la comida y cacharros, la vieja lavadora que con más de 35 años, aún sigue dando servicio. Ya no se fabrican como antes. Jodida obsolescencia programada. Ah! Tienes acceso al almacén en el Este." 
/11 "Estás en el Salon."
/12 "Estás en las escaleras. Puedes subir a la primera planta o bajar al descansillo."
/13 "Estás en el almacén. Ves una estantería casi vacía."
/14 "Estás en el patio."
/15 "Estás en una habitación secreta."
/16 "Estás en el primer tramo del pasillo. Es un pasillo largo."
/17 "Estás en el segundo tramo del pasillo."
/18 "Estás en el tercer tramo del pasillo."

;------------------------------------------------------------------------------
/CON    ;Conexiones
/0
/1
E lCoche
/2
E lExtcasa
/3
N lLenera
S lGarage
E lPorche
/4
S lExtcasa
/5
N lExtcasa
/6
O lExtcasa
/7
N lAseo
BAJAR lBodega
S lCocina
E lSalon
SUBIR lEscaleras
/8
SUBIR lDescansillo
/9
S lDescansillo
/10
E lAlmacen
N lDescansillo
/11
O lDescansillo
E lPatio
S lSecret2
/12
SUBIR lPasillo1
BAJAR lDescansillo
/13
O lCocina
/14
O lSalon
/15
N lSalon
/16
BAJAR lEscaleras
E lPasillo2
/17
O lPasillo1
E lPasillo3
/18
O lPasillo2

;------------------------------------------------------------------------------
/OBJ    ;Objetos
;obj  starts  weight    c w  5 4 3 2 1 0 9 8 7 6 5 4 3 2 1 0    noun   adjective
;num    at
/0      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LINTERNA    _
/1      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV1       _
/2      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV2       _
/3      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    BATERIA     _
/4      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    PINZAS      _
/5      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV0       _
/6      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    FELPUDO     _
/7      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    MACETA      _
/8      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    GNOMO       _
/9      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    CERILLAS    _
/10     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    TABLON      _
/11     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    SKEY1       _
/12     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    KCAJA       _
/13     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    MOVIL       _
/14     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    MECHERO     _
/15     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV3       _
/16     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    FOTOGRAFIA  _

;------------------------------------------------------------------------------
/PRO 0       ;Bucle principal de localidades

> _       _     AT      0               ; Empezamos una partida
                PROCESS 6               ; El proceso de inicialización

> _       _     WINDOW  0               ; Ventana de gráficos
                CLEAR   fDarkF           ; Asume que hay luz
                NOTZERO 0
                ABSENT  0
                SET     fDarkF           ; Oscuro

> _       _     PICTURE @fPlayer        ; Si hay gráfico, cárgalo
                DISPLAY @fDarkF         ; Muestralo si no oscuro; si oscuro
                                        ; hace un CLS
> _       _     WINDOW  1
                NOTZERO fDarkF           ; Oscuro
                SYSMESS 0

> _       _     ZERO    fDarkF
                DESC   @fPlayer        ; DESC No sale del bucle ahora

;> _       _     NEWLINE 
;                INK     6 
;                SYSMESS 63
;                INK     1 

> _       _     PROCESS 3

; Ahora usamos el proceso 1 como el bucle principal del juego. Un retorno desde
; aquí significa el fin del juego

> _       _     PROCESS 1

;------------------------------------------------------------------------------
/PRO 1

> _       _     INK     2               ; Color blanco para el texto narrado

> _       _     PROCESS 4               ; Lo que en el PAW era el proceso 2

> _       _     PARSE   0               ; Toma sentencia lógica del buffer
                PROCESS 2               ; Sentencia no válida o timeout
                REDO

> _       _     EQ      fTurns   255     ; El valo máximo de un byte
                PLUS    fTurnsHi   1
                CLEAR   fTurns
                SKIP    1

> _       _     PLUS    fTurns   1

> _       _     INK     15              ; Color verde para la entrada de acciones

> _       _     PROCESS 5               ; La antigua tabla de respuestas
                INK     15
                ISDONE                  ; Ha hecho algo?
                REDO                    ; Sí­: pedimos otra orden del jugador

> _       _     MOVE    fPlayer         ; No: intentamos mover al jugador
                RESTART                 ; Lo movimos: salta al principio de PRO 0

> _       _     NEWTEXT
                LT      fVerb    14
                INK     15
                SYSMESS 7
                REDO

> _       _     INK     15
                SYSMESS 8
                REDO


;------------------------------------------------------------------------------
/PRO 2

> _       _     INK     2

> _       _     HASAT TIMEOUT
                SYSMESS 35
                DONE

> _       _     SYSMESS 6               ; No te entiendo

;------------------------------------------------------------------------------
/PRO 3 ; Antiguo proceso 1 del PAW. (Nótese que tanto la tabla de respuestas como los
; antiguos procesos 1 y 2 del PAW ahora pueden estar en cualquer parte; o no
; existir en absoluto, ya que ya no hay nada automático al haberse implementado
; todo en el propio lenguaje del DAAD)
; Esta tabla se ejecuta tras describir las localidades, por lo cual es ideal para describir 
; cosas de las localidades que no sean fijas

;> _       _       AT lEscalera2
;                NOTZERO fAntor
;                MESSAGE "Cierta porción de la pared se ha movido dando acceso a otra sala hacia el oeste."

> _       _     NEWLINE
                ZERO    fDarkF            ; No está oscuro
                PROCESS 7
                LISTOBJ


;------------------------------------------------------------------------------
/PRO 4 ; Antiguo proceso 2 del PAW

> _       _     NOTZERO fLuz
                MINUS fLuz 1
                ZERO fLuz
                MESSAGE "#nLa cerilla se ha consumido."

/PRO 5 ; Interpretamos los comando como en la antigua tabla de respuestas

> SALID     _   PROCESS 7
                DONE

> USAR PINZAS    AT lMadrid
                GOTO lDescansillo
                RESTART
                DONE

; Meter un COGER MOVIL pero controlar que NO ha aparecido NOAT o algo asi, de modo que le suelte el mensaje de... "Pero dónde está móvil! Estoy un poco dormido. Debería echar un vistazo a la mesita."

> EXAMINA MESITA AT lMadrid
                MESSAGE "Es una mesita de noche sencilla, minimalista. Como tu sueles decir, una tabla con cuatro patas. Ahora bien, está muy limpia y sin rayaduras o arañazos.#n"
                ISAT oMovil 252                                
                MESSAGE "Hay un teléfono móvil en ella y no deja de sonar."
                CREATE oMovil
                DONE

> EXAMINA MESITA AT lMadrid
                NOTCARR oMovil
                MESSAGE "El teléfono sigue sonando"
                DONE  

> EXAMINA MESITA AT lMadrid
                DONE  

> COGER MOVIL AT lMadrid            
                ISAT oMovil 252
                MESSAGE "Estás muy adormilado como para saber de dónde viene el sonido del móvil. Mejor echa un vistazo a la mesita de noche."
                DONE

> USAR TELEFONO
                SYNONYM USAR MOVIL

> USAR MOVIL AT lMadrid
                CARRIED oMovil ; ¿lleva móvil?
                ZERO fUsarMovil
                SET fUsarMovil
                MESSAGE "+Buenos días hijo. Qué tal estás?.#n-Hasta que me llamaste estaba como un rey, en cama, calentito y tratando de negociar con Morpheo una segunda ronda.#n+Dile a Morpheo que espere un momento. Escucha, vente a la cabaña a pasar el día con nosotros, tenemos algo que contarte#n-Me apetece mucho, si... Pero no puede...#n+Hasta luego#n-Estupendo, no he podido poner un excusa, tengo que ir a ver mis padres."
                DONE 

> USAR MOVIL AT lMadrid
                NOTZERO fUsarMovil 
                MESSAGE "Tu padre ya ha colgado. Es mejor que muevas el culo y te vayas a por el coche (Este)."
                DONE

> MIRAR   _    AT lMadrid
                NOTCARR oMovil
                ZERO fUsarMovil
                MESSAGE "Estás aún en la cama, escuchando como suena el móvil. El sonido viene de la mesita de noche."
                DONE

> MIRAR   _    AT lMadrid
                CARRIED oMovil
                ZERO fUsarMovil
                MESSAGE "Estás aún en la cama, escuchando como suena el móvil. Tienes el móvil en la mano. Piensas usarlo para seguir la aventura o seguimos así todo el día? No serás un betatester, no?."
                DONE

> MIRAR   _    AT lMadrid ; Si ya has cogido el movil e insistes en mirar cosas
                CARRIED oMovil ; ¿lleva movil?
                NOTZERO fUsarMovil
                MESSAGE "Aquí ya no tienes nada que hacer. Tira para el coche: Este"
                DONE

> DORMIR  _    AT lMadrid
                NOTCARR oMovil ; No hemos cogido el movil
                ZERO fNoUsarMovil
                SET fNoUsarMovil
                MESSAGE "El móvil sigue sonando. No piensas cogerlo?"             
                DONE

> DORMIR  _    AT lMadrid
                NOTZERO fNoUsarMovil ; No hemos usado el movil
                NOTCARR oMovil ; No hemos cogido el movil
                MESSAGE "Nada en este mundo va a romper tu momento de paz y tranquilidad. Cuando te levantes, seguro que te hechas una partida al nuevo juego de MSX, Venganza#n"
                ;END ; Podemos acabar la partid con esto comando o podemos usar el siguiente
                END
                ;GOTO 0 ; vamos a la localización 0
                ;RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc

> EXAMINAR PLANTA
                SYNONYM EXAMINAR MACETA

> EXAMINAR MACETA AT lPorche
                NOTCARR oMaceta ; No ha cogido la maceta
                MESSAGE "Igual es mejor coger la maceta para verla más de cerca"
                DONE

> EXAMINAR MACETA AT lPorche
                CARRIED oMaceta ; Ha cogido la maceta
                ZERO fExaminarMaceta ; Activo un flag para controlar que YA examinó la maceta
                SET fExaminarMaceta
                MESSAGE "La maceta aún contiene arena y lo que en sus buenos tiempo sería una planta, que ahora mismo está más seca que la mojama."
                DONE

> EXAMINAR MACETA AT lPorche
                NOTZERO fExaminarMaceta 
                MESSAGE "No creo que vayas a sacar mucha más información de la maceta."
                DONE

> EXAMINAR FELPUDO AT lPorche
	            MESSAGE "Es un clásico felpudo de esparto con el mensaje WELCOME HOME."
                DONE

> EXAMINAR GNOMO AT lPorche
                NOTCARR oGnomo ; No ha cogido el gnomo
                MESSAGE "Es un gnomo rechoncho, con sonrisa picaresca y una mirada intensa."
                DONE
               
> EXAMINAR GNOMO AT lPorche
                CARRIED oGnomo
	            MESSAGE "Lo examinas con atención. Parece estar hecho de escayola, pesa poco. Posiblemente esté hueco por dentro. Lo agitas en el aire y ops! Algo pequeño golpea las paredes internas del gnomo. Buscas la apertura por la cual se introdujo el objeto dentro... sus pies, pero ahora están sellados con yeso. No se puede abrir."
                DONE

> MIRAR     _ AT lPorche
                ISAT oMaceta 252
                CREATE oMaceta
                ISAT oFelpudo 252
                CREATE oFelpudo
                ISAT oGnomo 252
                CREATE oGnomo
                MESSAGE "A tu alrededor puedes ver en el suelo una vieja maceta con lo que debió ser una planta, un felpudo y un gnomo de jardín."
                DONE

> COGER TODO AT lPorche
	            MESSAGE "Igual es mejor que vayas poco a poco. No puedes coger tantas cosas a la vez."
                DONE

> COGER FELPUDO AT lPorche
                ZERO fCogerFelpudo
                SET fCogerFelpudo
	            MESSAGE "Debajo del felpudo no hay nada a excepción de un cienpies que sale por patas al haber sido descubierto. Prefiero dejar el felpudo en su sitio."
                DONE

> COGER FELPUDO AT lPorche
                NOTZERO fCogerFelpudo
                NOTCARR oLlavePorche
	            MESSAGE "La llave de la casa no va a aparecer sola. Sigue buscando."
                DONE                

> TIRAR MACETA AT lPorche
                CARRIED oMaceta
	            MESSAGE "Tiras la maceta contra el suelo y no se rompe. De qué estará hecha?"
                DROP oMaceta
                DONE

> DEJAR MACETA AT lPorche
                CARRIED oMaceta
	            MESSAGE "Dejas la maceta en su sitio. Que buen hijo eres, si te viera tu madre estaría super orgullosa."
                DROP oMaceta
                DONE

> LANZAR GNOMO
                SYNONYM TIRAR GNOMO

> ROMPER GNOMO AT lPorche
                MESSAGE "Con las manos no puedo romperlo pero, si lo tiras al suelo?"
                DONE

> ABRIR GNOMO AT lPorche
                MESSAGE "Bajo los pies del gnomo hubo una apertura en su momento. Ahora hay yeso y no hay forma de quitarlo sin romper el gnomo."
                DONE

> TIRAR GNOMO AT lPorche
                ISAT oLlavePorche 252
                CREATE oLlavePorche                
            	MESSAGE "Tiras el gnomo contra el suelo y salta en mil pedazos. Parece ser que escondía una llave metálica. (Me encanta ese grupo)."
                GET oLlavePorche
                MESSAGE "Ahora bien, cuando se entere tu madre que has roto el gnomo... vas a flipar!!!."                
                DESTROY oGnomo
                DONE	

> ESCUCHAR METALICA at lPorche
	            XPLAY "T110O5G16F#16G16A16G8O4G4B4O5D8D8C8C4C16O4B16O5C16D16C8O4D4A4O5C8C8"
                XPLAY "T110O4B8B4O5G4.E16D8C#8C#8C#8G4.E16D8C#8C#8C#8G4.E16F#8D8O4B8"
                XPLAY "T110O5G8E4.D32E32D4"
                MESSAGE "Creo que me he confundido de música, perdón... intenta abrir la puerta y olvidemos este asunto."
                DONE

> ESCUCHAR METALICA at lCoche
	            XPLAY "T110O5G16F#16G16A16G8O4G4B4O5D8D8C8C4C16O4B16O5C16D16C8O4D4A4O5C8C8"
                XPLAY "T110O4B8B4O5G4.E16D8C#8C#8C#8G4.E16D8C#8C#8C#8G4.E16F#8D8O4B8"
                XPLAY "T110O5G8E4.D32E32D4"
                MESSAGE "Creo que me he confundido de música, perdón... sigue al Este y olvidemos este asunto."
                DONE

> EXAMINAR PUERTA at lPorche
                MESSAGE "Es una puerta fuerte, hecha de madera maciza. Como todas las puertas, tiene una cerradura para introducir una llave. Ahora recuerdas que siempre quisiste que arreglaran el timbre en casa, porque cuando todo el mundo está en las habitaciones, nadie escuchaba nada."
                DONE

> GOLPEAR PUERTA at lPorche
                MESSAGE "Nadie responde a tu llamada. Prueba otra cosa."
                DONE

> USAR LLAVE
                SYNONYM ABRIR PUERT

> ABRIR PUERTA  AT lPorche
                NOTCARR oLlavePorche
                MESSAGE "No puedes entrar la casa. La puerta está cerrada y necesitas la llave. Pero te viene a la memoria que, cuando tus padres salían a pasear por el bosque, solían esconder una llave en algún sitio. Lo recuerdas?"
                DONE

> ABRIR PUERTA	AT lPorche
                CARRIED oMaceta
                MESSAGE "Decides dejar la maceta donde estaba. Bastante tiene tu madre con haberle destrozado el gnomo."
                CARRIED oLlavePorche
            	MESSAGE "Ya estás dentro de la casa. Hace frío. Y apenas hay luz. Te has tropezado con la mesita de la entrada.#n- Hola? Hay alguien en casa?#n-No quiero bromitas porque sino me voy a cabrear y lo sabéis!#nNo responde nadie. La puerta se cierra tras de tí de un portazo. Ya no se abre. Está atascada y por tanto, estás encerrado.#n"
                DESTROY oMaceta
                goto lDescansillo
                RESTART
                DONE

> ABRIR PUERTA	AT lPorche
                CARRIED oLlavePorche
            	MESSAGE "Ya estás dentro de la casa. Hace frío. Y apenas hay luz. Te has tropezado con la mesita de la entrada.#n- Hola? Hay alguien en casa?#n-No quiero bromitas porque sino me voy a cabrear y lo sabéis!#nNo responde nadie. La puerta se cierra tras de tí de un portazo. Ya no se abre. Está atascada y por tanto, estás encerrado.#n"
                goto lDescansillo
                RESTART
                DONE

; Ya estamos dentro de la casa y no podemos salir al exterior

> MIRAR     _   AT lDescansillo
                MESSAGE "Hay poca luz. Puedes ver las escaleras que suben a las habitaciones de la planta superior, el baño de la planta de abajo, la entrada a la cocina y enfrente el salón. Y por supuesto, la mesita con la que has tropezado."
                DONE

> EXAMINAR MESITA AT lDescansillo
                ZERO fMesita
                SET fMesita
                MESSAGE "Es una preciosa mesita de madera con patas forjadas de hierro. Tu madre la heredó de sus padres. Tiene un cajón semiabierto."
                DONE

> EXAMINAR MESITA AT lDescansillo
                NOTZERO fMesita
                ZERO fCajon
                MESSAGE "La mesita sigue siendo preciosa. Abres el cajón y seguimos?"
                DONE

> EXAMINAR MESITA AT lDescansillo
                NOTZERO fMesita
                NOTZERO fCajon
                MESSAGE "La mesita sigue siendo preciosa. Seguimos?"
                DONE

> ABRIR CAJON   AT lDescansillo
                ISAT oLlave1 252
                ISAT oLlave2 252                
                ISAT oCerillas 252                
                MESSAGE "En el cajón de la mesita puedes ver las llaves del garage y de la leñera, además de unas cerillas. Lo coges todo!!!#n"   
                CREATE oLlave1
                GET oLlave1
                CREATE oLlave2
                GET oLlave2
                CREATE oCerillas
                GET oCerillas
                ZERO fCerillas
                SET fCerillas
                LET fCerillas 15
                ZERO fCajon
                SET fCajon
	            MESSAGE "#nLa caja contiene 15 cerillas."
                DONE            

> ABRIR CAJON AT lDescansillo
                NOTZERO fCajon
                MESSAGE "Ya no hay nada interesante en la mesita."
                DONE

> EXAMINAR CERILLAS AT lDescansillo
                CARRIED oCerillas
                MESSAGE "Es una caja de cerillas de palo largo, típicas para encender los fogones de la cocina y velas. Contiene 15 cerillas."
                DONE

> BAJAR BODEGA AT lDescansillo
                MESSAGE "Necesitas la llave de la bodega para poder entrar."
                DONE

> USAR CERILLA 
                CARRIED oCerillas
                NOTEQ fCerillas 0
                LET fLuz 4
                MINUS fCerillas 1
                MESSAGE "Las cerillas funcionan y ves bastante mejor. Pero ojo, se consumen.#nTe quedan:"
                PRINT fCerillas
                DONE

> S         _  AT lDescansillo ; No podemos entrar en la cocina sin LUZ
                ZERO fLuz
                SET fDark
                MESSAGE "La cocina está muy oscura. Sin luz no es buena idea entrar. Necesitas algo para iluminar."
                DONE

> S         _  AT lDescansillo ; Tenemos LUZ y nos metemos en la cocina
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                CARRIED oLinterna
                ;CLEAR fDark
                MESSAGE "Enciendes la linterna para entrar en la cocina."
                GOTO lCocina
                RESTART
                DONE


> USAR INTERRUPTOR AT lAseo
                ZERO fUsarInterruptor
                SET fUsarInterruptor
                ISAT oFotografia1 252
                CREATE oFotografia1
                MESSAGE "Pulsas el interruptor de la luz y ves una fotografía en el suelo pero la luz comienza a parpadear con rapidez y la bombilla salta por los aires. Te has sobresaltado un poco, la verdad."
                DONE

> USAR INTERRUPTOR AT lAseo
                NOTZERO fUsarInterruptor
                MESSAGE "Obviamente no funciona. Hasta dónde llegan tus conocimientos de electricidad? Si no hay bombilla... no puede haber luz."
                DONE

> COGER FOTOGRAFIA AT lAseo
                NOTZERO fUsarInterruptor
                GET oFotografia1
                MESSAGE "#nAún a pesar del susto y con sumo cuidado para no cortarte con los cristales de la bombilla.#nAl cogerla has sentido como si algo te tocara la mano, algo muy frío. Se te han puesto los pelos como escarpias. Ahora SI que te has asustado. Podrías encender algo para iluminarte y ver que te ha tocado, o mejor salimos del aseo y olvidamos el asunto."
                DONE

> USAR LINTERNA AT lAseo
	            MESSAGE "No debiste hacer eso. Lo que pareció ser un niño en sus buenos momentos, te lanza con fuerza contra el lavabo y oyes un fuerte CRACK en tu cabeza. Caes al suelo como un muñeco. Lo último que pasa por tu mente es el sonido de tu sangre gorgotear. Has muerto."
                END

> EXAMINAR LINTERNA
                MESSAGE "Es una linterna moderna, no ilumina mucho pero es una ayuda. Ojo con las baterías, porque se agotan."
                DONE

> EXAMINAR ASEO AT lAseo
	            CARRIED oLinterna               
            	MESSAGE "No se ve nada. Y no puedes encender la luz, ya que no hay bombilla. Olvídate del aseo y sal de aquí."
                DONE

> EXAMINAR ASEO AT lAseo
                NOTCARR oLinterna
                MESSAGE "No se ve nada. Y no puedes encender la luz, ya que no hay bombilla. Pero tocas una linterna en el suelo."
                DONE

> MIRAR     _ AT lCocina
                CLEAR fDark
                MESSAGE "Puedes ver el fregadero, un mueble con cajones y la vieja lavadora que con 35 años, aún sigue dando servicio. Ya no se fabrican como antes."
                DONE

> EXAMINAR FREGADERO AT lCocina
                ISAT oLlavePatio 252
                MESSAGE "El fregadero está vacío, tiene un sumidero grande tipo americano. Algo parece estar atascado ahí dentro."
                DONE

> EXAMINAR SUMIDERO AT lCocina
                MESSAGE "Te alzas en el fregadero para poder meter mejor la mano en el sumidero. Vas introduciendo la mano poco a poco, es un proceso delicado, pues podría caerse lo que hubiera allí atascado hasta el fondo de la cañería y darlo por perdido. Tus dedos ya rozan el objeto. Con la punta de los dedos consigues cogerlo y según comienzas a sacar los dedos oyes que se pone el marcha el triturador del fregadero. Por el susto y la excitación sacas tu mano a toda velocidad y el objeto que tenías cogido por los dedos sale por los aires. Echas las dos manos al aire para coger el objeto al vuelo y de tu mano izquierda salta la caja de cerillas por los aires también. Mierda! Ahora tienes dos objetos en los aires y tus brazos en alto esperando coger alguno. Ves que el objeto del sumidero son unas... llaves, así que por instinto, echas las manos hacia ellas para asegurar su rescate. Las cerillas caen en el sumidero. Oyes como el triturador se hace cargo de sus destrucción."
                CREATE oLlavePatio
                DESTROY oCerillas
                GET oLlavePatio
                DONE

> EXAMINAR MUEBLE AT lCocina
                MESSAGE "Es un típico mueble de cocina hecho en madera con varios cajones. Mis padres lo compraron en el rastro de Madrid en no muy buenas condiciones pero a un fantástico precio. La idea de mi padre era restaurarlo y vaya si lo hizo!"
                DONE

> EXAMINAR CAJONES AT lCocina
                MESSAGE "Puedes ver una linterna.#nEs curioso, no hay cuchillos en los cajones."
                CREATE oLinterna
                GET oLinterna
                MESSAGE "Decides"
                DONE

> EXAMINAR LAVADORA AT lCocina
                MESSAGE "Dentro encuentras un cuchillo grande de carnicero.#n¿Pero que puñetas hace eso ahí dentro?"
                DONE

> E         _  AT lCocina ; No podemos entrar en el almacén sin LUZ
                SET fDark
                MESSAGE "El almacén está muy oscura. Sin luz no se puede entrar."
                DONE

> E         _  AT lCocina ; Tenemos LUZ y nos metemos en el almacén
                CLEAR fDark
                GOTO lAlmacen
                RESTART
                MESSAGE ""
                DONE

> ABRIR MUEBLE AT lAlmacen
                ISAT oLinterna 252
                CREATE oLinterna
                GET oLinterna
                DONE

> SUBIR     _  AT lDescansillo
                SET fDark
                ZERO fTablon
                NOTCARR oLinterna
                MESSAGE "Las escaleras están muy oscuras. Necesitas algo para iluminar."
                DONE

> SUBIR     _  AT lDescansillo
                NOTZERO fTablon                
                MESSAGE "Metes el pie accidentalmente en el escalón al que quitaste el tablón y ;tropiezas malamente. Caes rodando las escaleras y te partes el cuello. Has muerto."
                INKEY
                GOTO 0 ; vamos a la localización 0
                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc
                DONE

> SUBIR     _  AT lEscaleras
                SET fDark                
                NOTZERO fTablon                
                MESSAGE "Metes el pie accidentalmente en el escalón al que quitaste el tablón y ;tropiezas malamente. Caes rodando las escaleras y te partes el cuello. Has muerto."
                GOTO 0 ; vamos a la localización 0
                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc
                DONE

> SUBIR     _  AT lDescansillo
                ZERO fTablon
                CLEAR fDark
                CARRIED oLinterna
                GOTO lEscaleras
                ;RESTART
                MESSAGE "#nNotas un pequeño crujido bajo tus pies. Siempre ha habido un tablón suelto en esa escalera y nadie se preocupó de arreglarlo."
                DONE

> SUBIR     _  AT lEscaleras
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> SUBIR     _  AT lEscaleras
                CLEAR fDark
                CARRIED oLinterna
                ZERO fTablon
                MESSAGE "Menos mal que llevas algo de luz. Toda la planta superior está a oscuras."
                GOTO lPasillo1
                RESTART
                DONE

> EXAMINAR ESCALERAS AT lEscaleras
                CLEAR fDark
                CARRIED oLinterna
                MESSAGE "Sientes curiosidad por examinar la escalera con más detalle."
                ISAT oTablon 252
                MESSAGE "Hay un tablón que parece tener cierta holgura y se mueve un poco."
                CREATE oTablon
                DONE

> EXAMINAR TABLON AT lEscaleras
                CLEAR fDark
                MESSAGE "Efectivamente está un poco suelto. Creo que podría moverlo."
                DONE

> EXAMINAR TABLON AT lEscaleras
                NOTZERO fTablon
                MESSAGE "El escalón está abierto, no has puesto el tablón en su sitio."
                DONE

> MOVER TABLON AT lEscaleras
                ZERO fTablon
                SET fTablon
                MESSAGE "Mueves el tablón y lo desplazas hacia un lado. Puedes ver una caja de metal dentro."
                DONE

> MOVER TABLON AT lEscaleras
                NOTZERO fTablon                
                MESSAGE "El tablón no se mueve más"                           

> EXAMINAR CAJA AT lEscaleras
                MESSAGE "La caja tiene un candado de seguridad. Necesitas una llave."
                DONE

> COGER CAJA AT lEscaleras
                MESSAGE "La caja está pegada dentro del hueco del escalón, no se puede coger."
                DONE

> ABRIR CAJA AT lEscaleras
                NOTCARR oLlaveCaja
                MESSAGE "No tienes la llave. No puedes abrirla."
                DONE

> ABRIR CAJA AT lEscaleras
                CARRIED oLlaveCaja
                ISAT oSecretKey1 252
                CREATE oSecretKey1
                MESSAGE "Has conseguido abrir la caja." 
                GET oSecretKey1
                MESSAGE ""¿Que abrirá esa tarjeta de seguridad?""
                DONE

> PONER TABLON AT lEscaleras
                NOTZERO fTablon
                CLEAR fTablon
                MESSAGE "Has puesto el tablón en su sitio."
                DONE                

> PONER TABLON AT lEscaleras
                ZERO fTablon
                MESSAGE "Has puesto el tablón en su sitio."
                DONE      

> BAJAR     _  AT lEscaleras
                CLEAR fDark
                ZERO oTablon
                GOTO lDescansillo
                MESSAGE "Has vuelto al descansillo."
                DONE

> E     _      AT lPasillo1 
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> E     _      AT lPasillo1
                CLEAR fDark
                CARRIED oLinterna
                GOTO lPasillo2
                RESTART
                DONE

> BAJAR     _  AT lPasillo1
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> BAJAR     _  AT lPasillo1
                ZERO fTablon
                CLEAR fDark
                CARRIED oLinterna
                GOTO lEscaleras
                ;RESTART
                MESSAGE "#nNotas un pequeño crujido bajo tus pies. Siempre ha habido un tablón suelto en esa escalera y nadie se preocupó de arreglarlo."
                DONE

> E     _      AT lPasillo2
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> E     _      AT lPasillo2
                CLEAR fDark
                CARRIED oLinterna
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo3
                RESTART
                DONE

> O     _      AT lPasillo2
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> O     _      AT lPasillo2
                CLEAR fDark
                CARRIED oLinterna
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo1
                RESTART
                DONE

> O     _      AT lPasillo3
                NOTCARR oLinterna
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> O     _      AT lPasillo3
                CLEAR fDark
                CARRIED oLinterna
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo2
                RESTART
                DONE

> E     _      AT lPorche
                NOTCARR oLlavePorche
                MESSAGE "No puedes entrar la casa. La puerta está cerrada y necesitas la llave. Pero recuerdas que cuando tus padres salían a pasear por el bosque, solían esconder una llave en algún sitio."
                DONE

> E     _      AT lPorche 
                CARRIED oLlavePorche
            	MESSAGE "Ya estás dentro de la casa. Hace frío. Y apenas hay luz. Te has tropezado con la mesita de la entrada.#n- Hola? Hay alguien en casa?#n-No quiero bromitas porque sino me voy a cabrear y lo sabéis!#nNo responde nadie."
                goto lDescansillo
                DONE

> E     _      AT lMadrid ; Si no has usado el movil y quieres salir al Este
                Zero fUsarMovil
                MESSAGE "No crees que deberías atender al móvil?"
                DONE

> E     _      AT lMadrid ; Si no has usado el movil y quieres salir al Este
                NOTZERO fUsarMovil
                CARRIED oMovil
                MESSAGE "Te duchas y vistes con rapidez, sabes que a tu padre no le gusta que le hagan esperar. Coges las llaves del coche cierras la puerta de casa y vas directo al garage."
                DESTROY oMovil
                INKEY
                CLS
                GOTO lCoche
                RESTART
                DONE

> E     _      AT lCoche
              CLS
              GOTO lExtcasa
              RESTART
              DONE

> N     _      AT lExtcasa
                Zero fPuertaLenera
                MESSAGE "No puedes entrar en la leñera. La puerta está cerrada. Necesitas una llave."
                DONE

> S     _      AT lExtcasa
                Zero fPuertaGarage
                MESSAGE "No puedes entrar en el garage. La puerta está cerrada.Necesitas una llave."
                DONE

> I       _     SYSMESS 9
                LISTAT  CARRIED
                SYSMESS 10
                LISTAT  WORN
                DONE

> COGER   TODO  DOALL   HERE                
> COGER   _     AUTOG
                DONE

> DEJAR   TODO  DOALL   CARRIED
> DEJAR   _     AUTOD
                DONE

> QUITAR  TODO  DOALL   WORN
> QUITAR  _     AUTOR
                DONE

> PONER   TODO  DOALL   CARRIED
> PONER   _     AUTOW
                DONE

> R       _     RESTART

> FIN     _     QUIT
                END

> FIN     _     DONE                    

> SV      _     SAVE 0                  
                RESTART

> LD      _     LOAD 0
                RESTART

> RAMSA   _     RAMSAVE
                RESTART

> RAMLO   _     RAMLOAD 255                             ;Reload all flags
                RESTART

> MIRAR   _     RESTART


/PRO 6 ; Inicialización

> _       _     WINDOW  0               ; Window para imágenes
                WINAT   0       0
                WINSIZE 14      127     ; 14 filas de alto * 8 pixels por fila = 112 pixels
                PICTURE 0
                DISPLAY 0

> _       _     WINDOW  1               ; Window para los textos
                WINAT   15      0
                WINSIZE 25      127     ; Tamaño máximo
                CLS
                DESC    0               ; Introducción
                ANYKEY
                CLS
                SET     255             ; Pone a cero todas las banderas

> _       _     MINUS   255     1
                NOTEQ   255     fGFlags ; menos GFlags!!!
                CLEAR   @255

> _       _     NOTZERO 255
                SKIP    -2

> _       _     RESET                   ; Objetos a su loc. inicial / Flag 1
                LET     fStrength 10
                LET     fMaxCarr  4
                SET     fCPronounNoun
                SET     fCPronounAdject
                GOTO    1               ; Primera localidad del juego

/PRO 7

; Listado automático de salidas
; ¡OJO!, gasta los flags 100 y 101

>
_       _       MES 0
                CLEAR 101
                LET 33 2

>
_       _       COPYFF 38 100
                MOVE 100
                MES @33
                PLUS 101 1

>
_       _       PLUS 33 1
                LT 33 14
                SKIP -2

>
_       _       NOTZERO 101
                DONE

>
_       _       MESSAGE 1

/END
