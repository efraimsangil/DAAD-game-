;------------------------------------------------------------------------------------------
;ÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑ
;ÑÑÑÑÑ                                                                                  ÑÑÑÑÑ
;ÑÑÑÑÑ Este es el fichero de inicio de la librería para DAAD Reborn Compiler en español ÑÑÑÑÑ
;ÑÑÑÑÑ                                                                                  ÑÑÑÑÑ
;ÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑ

; Por favor no quites las eñes en el recuadro justo encima, están ahí para que notes fácil-
; mente si abres el fichero con una codificación que no es la correcta (ISO-8859-1 o Win1252)

/CTL    ;Sección de control
_
;#extern "MLV_MSX.BIN"
;#classic
;#debug

#define TIMEOUT   87            ;If Timeout last frame

; System flags 0-63
#define fDark               0
#define fObjectsCarried     1

#define fDarkF              28
#define fGFlags             29     ; This is best tested using HASAT GMODE
#define fScore              30
#define fTurns              31    ; 2 bytes
#define fTurnsHi            32

#define fVerb               33
#define fNoun               34
#define fAdject1            35
#define fAdverb             36
#define fMaxCarr            37
#define fPlayer             38
#define fPrep               43
#define fNoun2              44
#define fAdject2            45
#define fCPronounNoun       46
#define fCPronounAdject     47
#define fTimeout            48
#define fTimeoutFlags       49
#define fDoallObjNo         50
#define fRefObject          51
#define fStrength           52
#define fObjFlags           53
#define fRefObjLoc          54
#define fRefObjWeight       55
#define fRefObjIsContainer  56
#define fRefObjisWearable   57
#define fRefObjAttr1        58
#define fRefObjAttr2        59
#define fInkeyKey1          60
#define fInkeyKey2          61
#define fScreenMode         62     ; 2=Text, 4=CGA, 13=EGA, 141=VGA
#define fCurrentWindow      63     ; Which window is active at the moment

;Flags de usuario   
#define fPuertaLenera       64
#define fPuertaGarage       65
#define fPuertaPorche       66
#define fUsarMovil          67
#define fNoUsarMovil        68
#define fExaminarMaceta     69
#define fCogerFelpudo       70
#define fCogerGnomo         71
#define fCerillas           72
#define fLuz                73
#define fCajon              74
#define fMesita             75
#define fTablon             76


;localidades
#define lIntro       0
#define lMadrid      1
#define lCoche       2
#define lExtcasa     3
#define lLenera      4
#define lGarage      5
#define lPorche      6
#define lDescansillo 7
#define lBodega      8
#define lAseo        9
#define lCocina      10
#define lSalon       11
#define lEscaleras   12
#define lAlmacen     13
#define lPatio       14
#define lSecret2     15
#define lPasillo1    16
#define lPasillo2    17
#define lPasillo3    18

;Objetos
#define oMovil          0
#define oLlave1         1
#define oLlave2         2
#define oBateriaCoche   3
#define oPinzasCoche    4
#define oLlavePorche    5
#define oFelpudo        6
#define oMaceta         7
#define oGnomo          8
#define oCerillas       9
#define oTablon         10
#define oSecretKey1     11
#define oLlaveCaja      12
#define oLinterna       13

;------------------------------------------------------------------------------
/VOC    ;Vocabulario
;                       Movimiento: verbos y nombres < 14
N       2       noun
NORTE   2       noun
S       3       noun
SUR     3       noun
E       4       noun
ESTE    4       noun
O       5       noun
OESTE   5       noun
NE      6       noun
NO      7       noun
SE      8       noun
SO      9       noun
AR      10      noun
ARRIBA  10      noun
SUBIR   10      verb
AB      11      noun
ABAJO   11      noun
BAJAR   11      verb

;                       Nombres <20 indica que se pueden usar como verbos
;                               <50 indica nombre propio (no admite lo, la...)
I       14      noun
INVEN   14      noun
TODO    20      noun
TODOS   20      noun
TODAS   20      noun

; Objetos
MESITA   99      noun
MOVIL    100     noun
LLAV1    101     noun
LLAV2    102     noun
BATERIA  103     noun
PINZAS   104     noun
LLAV0    105     noun
FELPUDO  106     noun
MACETA   107     noun
GNOMO    108     noun
PORCHE   109     noun
METALICA 110     noun
PUERTA   111     noun
CAJON    112     noun
CERILLAS 113     noun
BODEGA   114     noun
ESCALERAS 115    noun
LLAVE     116    noun
TABLON    117    noun
CAJA      118    noun
TARJETA   119    noun
LLAV3     120    noun
SKEY1     121    noun
KCAJA     122    noun
LINTERNA  123    noun
MUEBLE    124    noun
FREGADERO 125    noun
LAVADORA  125    noun

;                       Verbos
COGER   20      verb
DEJAR   21      verb
SOLTAR  21      verb
QUITAR  22      verb
PONER   23      verb
R       24      verb
M       24      verb
MIRAR   24      verb
REDES   24      verb
F       25      verb
FIN     25      verb
SAVE    26      verb
SV      26      verb
LOAD    27      verb
LD      27      Verb
RAMSA   28      verb
RAMLO   29      verb
EX      30      verb
EXAMI   30      verb
DECIR   31      verb
PREGUNT 31      verb
HABLAR  31      verb
JUGAR   32      verb

;verbos jugador
USAR     33      verb
DORMIR   34      verb
TIRAR    35      verb
ESCUCHAR 36      verb
ENCENDER 37      verb
ABRIR    38      verb
PRENDER  39      verb
MOVER    40      verb
GOLPEAR  41      verb

;                            Adjetivos
PEQUE   2       adjective
GRANDE  3       adjective
VIEJO   4       adjective
VIEJA   4       adjective
NUEVO   5       adjective
NUEVA   5       adjective
DURO    6       adjective
DURA    6       adjective
BLANDO  7       adjective
CORTO   8       adjective
CORTA   8       adjective
LARGO   9       adjective
LARGA   9       adjective
;                               Adverbios
RAPID   2       adverb
DESPA   3       adverb
LENTA   3       adverb
CUIDAd  5       adverb
;                               Prepositions
A       2       preposition
DE      3       preposition
EN      4       preposition
POR     5       preposition
TRAVES  5       preposition
BAJO    7       preposition
JUNTO   8       preposition
MENOS   13      preposition
;                               Pronombres
;                               Conjunciones
Y       2       conjugation
ENTONCE 2       conjugation
LUEGO   2       conjugation


;------------------------------------------------------------------------------
/STX    ;Mensajes del sistema
/0 "Está demasiado oscuro para ver nada."
/1 "También puedo ver:"
/2 "#n"
/3 "#n"
/4 "#n"
/5 "#n"
/6 "No he entendido nada."
/7 "No puedo ir en esa dirección."
/8 "No puedo hacer eso."
/9 "Llevo:#n"
/10 "Llevo puesto:#n"
/11 "";*Libre
/12 "¿Seguro?"
/13 "¿Quieres intentarlo de nuevo?"
/14 ""  ;*Libre
/15 "Vale."
/16 "Pulsa una tecla para continuar."
/17 "" ;*Has jugado
/18 "" ;*\sturno
/19 "" ;*s
/20 "" ; *.
/21 "" ; *Ha resuelto un
/22 "" ; *%
/23 "No llevo eso puesto."
/24 "No puedo. Tengo puesto _."
/25 "Ya tengo _."
/26 "Aquí no hay nada de eso."
/27 "No puedo llevar nada más."
/28 "No tengo eso.#n"
/29 "Ya llevo puesto _.#n"
/30 "S";Un solo carácter en mayúsculas. Inicial de Sí
/31 "N"  ;Un solo carácter en mayúsculas. Inicial de No
/32 "Hay más..."
/33 ">"
/34 "" ;*Libre
/35 "#nEl tiempo pasa..."
/36 "He cogido _."
/37 "Me he puesto _."
/38 "Me he quitado _."
/39 "He dejado _."
/40 "No puedo ponerme _."
/41 "No puedo quitarme _."
/42 "No puedo quitarme _.  No puedo llevar nada más en las manos."
/43 "@ pesa demasiado para mí."
/44 "@ está en"
/45 "@ no está en"
/46 ", "
/47 " y "
/48 ".#n"
/49 "No tengo _."
/50 "No llevo puesto _."
/51 ".#n"
/52 "No hay nada de eso en"
/53 "Nada.#n"
/54 "C" ; Inicial de Cinta
/55 "D" ; Disco
/56 "Unidad no preparada. Pulsa una tecla para volver a intentarlo."
/57 "Error de entrada/salida."
/58 "El disco o el directorio puede estar lleno."
/59 "Nombre de fichero no válido."
/60 "Nombre del fichero:"
/61 "Pon en marcha la cinta."
/62 "¿Cinta o disco?"


;------------------------------------------------------------------------------
/MTX    ;Mensajes
/0 ""


;------------------------------------------------------------------------------
/OTX    ;Object Texts
/0 "un telefono movil"
/1 "llave de la leñera"
/2 "llave del garage"
/3 "una bateria de coche"
/4 "unas pinzas de coche"
/5 "una llave de la entrada principal"
/6 "un felpudo"
/7 "una maceta"
/8 "un Gnomo"
/9 "una caja de cerillas"
/10 "una tarjeta de seguridad"
/11 "una llave dorada"
/12 "llave de una caja pequeña"
/13 "una linterna"

;------------------------------------------------------------------------------
/LTX    ;Location Texts
/0 "Es sábado, son las 08:00AM y aún sigues en la cama. Semi despierto y sin ganas de levantarte. Madrugas mucho durante la semana y no te apetece mucho abandonar el calorcito que sientes bajo el plumón.#n#n"
/1 "Te giras en la cama con la firme intención de dormir un poquito más pero, suena el teléfono móvil. Alguien ya decidió por ti que te debes despertar del todo. Tienes a tu lado la mesita de noche."
/2 "Sales de casa y coges tu coche rumbo a la sierra de gredos desde Madrid. Es un paseito, pero el viaje se hace ameno escuchando un poco de música. Vete al Este"
/3 "Estás en el exterior de la casa. Desde aquí puedes ir al Norte, Sur y Este"
/4 "Estás en la leñera. Puedes entrar o volver al Sur"
/5 "Lograste entrar en el garage. Que fácil es todo cuando tienes las llaves"
/6 "Un par de potentes luces iluminan el porche. Nunca te gustaron esas luces porque atraín muchos insectos las noches de verano y era imposible estar en el porche sin sufrir el feroz ataque de un mosquito sediento de sangre."
/7 "Estás en el descansillo."
/8 "Estás en la bodega."
/9 "Estás en el aseo."
/10 "Estás en la cocina." 
/11 "Estás en el Salon."
/12 "Estás en las escaleras. Puedes subir a la primera planta o bajar al descansillo."
/13 "Estás en el almacén. Ves una estantería casi vacía."
/14 "Estás en el patio."
/15 "Estás en una habitación secreta."
/16 "Estás en el primer tramo del pasillo. Es un pasillo largo. Salidas: N E BAJAR"
/17 "Estás en el segundo tramo del pasillo. Salidas: N E O"
/18 "Estás en el tercer tramo del pasillo. Salidas: N O"

;------------------------------------------------------------------------------
/CON    ;Conexiones
/0
/1
E lCoche
/2
E lExtcasa
/3
N lLenera
S lGarage
E lPorche
/4
S lExtcasa
/5
N lExtcasa
/6
O lExtcasa
/7
N lAseo
BAJAR lBodega
S lCocina
E lSalon
SUBIR lEscaleras
/8
N lDescansillo
/9
S lDescansillo
/10
E lAlmacen
N lDescansillo
/11
O lDescansillo
E lPatio
S lSecret2
/12
SUBIR lPasillo1
BAJAR lDescansillo
/13
O lCocina
/14
O lSalon
/15
N lSalon
/16
BAJAR lEscaleras
E lPasillo2
/17
O lPasillo1
E lPasillo3
/18
O lPasillo2

;------------------------------------------------------------------------------
/OBJ    ;Objetos
;obj  starts  weight    c w  5 4 3 2 1 0 9 8 7 6 5 4 3 2 1 0    noun   adjective
;num    at
/0      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    MOVIL       _
/1      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV1       _
/2      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV2       _
/3      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    BATERIA     _
/4      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    PINZAS      _
/5      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LLAV0       _
/6      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    FELPUDO     _
/7      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    MACETA      _
/8      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    GNOMO       _
/9      252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    CERILLAS    _
/10     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    TABLON      _
/11     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    SKEY1       _
/12     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    KCAJA       _
/13     252     1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LINTERNA    _

;------------------------------------------------------------------------------
/PRO 0       ;Bucle principal de localidades

> _       _     AT      0               ; Empezamos una partida
                PROCESS 6               ; El proceso de inicialización

> _       _     WINDOW  0               ; Ventana de gráficos
                CLEAR   fDarkF           ; Asume que hay luz
                NOTZERO fDark
                ABSENT  oLinterna
                SET     fDarkF           ; Oscuro

> _       _     PICTURE @fPlayer        ; Si hay gráfico, cárgalo
                DISPLAY @fDarkF         ; Muestralo si no oscuro; si oscuro
                                        ; hace un CLS
> _       _     WINDOW  1
                NOTZERO fDarkF           ; Oscuro
                SYSMESS 0

> _       _     ZERO    fDarkF
                DESC   @fPlayer        ; DESC No sale del bucle ahora

> _       _     PROCESS 3

; Ahora usamos el proceso 1 como el bucle principal del juego. Un retorno desde
; aquí significa el fin del juego

> _       _     PROCESS 1

;------------------------------------------------------------------------------
/PRO 1

> _       _     PROCESS 4               ; Lo que en el PAW era el proceso 2

> _       _     PARSE   0               ; Toma sentencia lógica del buffer
                PROCESS 2               ; Sentencia no válida o timeout
                REDO

> _       _     EQ      fTurns   255     ; El valo máximo de un byte
                PLUS    fTurnsHi   1
                CLEAR   fTurns
                SKIP    1

> _       _     PLUS    fTurns   1

> _       _     PROCESS 5               ; La antigua tabla de respuestas
                ISDONE                  ; Ha hecho algo?
                REDO                    ; Sí­: pedimos otra orden del jugador

> _       _     MOVE    fPlayer         ; No: intentamos mover al jugador
                RESTART                 ; Lo movimos: salta al principio de PRO 0

> _       _     NEWTEXT
                LT      fVerb    14
                SYSMESS 7
                REDO

> _       _     SYSMESS 8
                REDO


;------------------------------------------------------------------------------
/PRO 2
> _       _     HASAT TIMEOUT
                SYSMESS 35
                DONE

> _       _     SYSMESS 6               ; No te entiendo

;------------------------------------------------------------------------------
/PRO 3 ; Antiguo proceso 1 del PAW. (Nótese que tanto la tabla de respuestas como los
; antiguos procesos 1 y 2 del PAW ahora pueden estar en cualquer parte; o no
; existir en absoluto, ya que ya no hay nada automático al haberse implementado
; todo en el propio lenguaje del DAAD)
; Esta tabla se ejecuta tras describir las localidades, por lo cual es ideal para describir 
; cosas de las localidades que no sean fijas

;> _       _       AT lEscalera2
;                NOTZERO fAntor
;                MESSAGE "Cierta porción de la pared se ha movido dando acceso a otra sala hacia el oeste."

> _       _     NEWLINE
                ZERO    fDarkF            ; No está oscuro
                LISTOBJ


;------------------------------------------------------------------------------
/PRO 4 ; Antiguo proceso 2 del PAW

> _       _     NOTZERO fLuz
                MINUS fLuz 1
                ZERO fLuz
                MESSAGE "la cerilla se ha consumido"

/PRO 5 ; Interpretamos los comando como en la antigua tabla de respuestas

> USAR PINZAS    AT lMadrid
                GOTO lDescansillo
                RESTART
                DONE

> EXAMINA MESITA AT lMadrid
                MESSAGE "La mesita de noche está limpia y perfecta."
                ISAT oMovil 252
                MESSAGE "Hay un teléfono móvil en ella."
                CREATE oMovil
                DONE

> EXAMINA MESITA AT lMadrid
                NOTCARR oMovil
                MESSAGE "El teléfono sigue sonando"
                DONE  

> EXAMINA MESITA AT lMadrid
                DONE  

> USAR  MOVIL AT lMadrid
                CARRIED oMovil ; ¿lleva móvil?
                MESSAGE "+Buenos días hijo. Qué tal estás?.#n-Hasta que me llamaste estaba como un rey, en cama, calentito y tratando de negociar con Morpheo una segunda ronda.#n+Dile a Morpheo que espere un momento. Escucha, vente a la cabaña a pasar el día con nosotros, tenemos algo que contarte#n-Me apetece mucho, si... Pero no puede...#n+Hasta luego#n-Estupendo, no he podido poner un excusa, tengo que ir a ver mis padres."
                SET fUsarMovil
                DONE 

> USAR MOVIL AT lMadrid
                NOTZERO fUsarMovil 
                MESSAGE "Tu padre ya ha colgado. Es mejor que muevas el culo y te vayas a por el coche."
                DONE

> MIRAR   _    AT lMadrid ; Si ya has cogido el movil e insistes en mirar cosas
                CARRIED oMovil ; ¿lleva movil?
                NOTZERO fUsarMovil
                MESSAGE "Aquí ya no tienes nada que hacer. Tira para el coche: Este"
                DONE

> DORMIR  _    AT lMadrid
                NOTCARR oMovil ; No hemos cogido el movil
                ZERO fNoUsarMovil
                SET fNoUsarMovil
                MESSAGE "El móvil sigue sonando. No piensas cogerlo?"             
                DONE

> DORMIR  _    AT lMadrid
                NOTZERO fNoUsarMovil ; No hemos usado el movil
                NOTCARR oMovil ; No hemos cogido el movil
                MESSAGE "Nada en este mundo va a romper tu momento de paz y tranquilidad. Cuando te levantes, seguro que te hechas una partida al nuevo juego de MSX, Venganza"
                ;END ; Podemos acabar la partid con esto comando o podemos usar el siguiente
                GOTO 0 ; vamos a la localización 0
                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc

> EXAMINAR MACETA AT lPorche
                NOTCARR oMaceta ; No ha cogido la maceta
                MESSAGE "Igual es mejor coger la maceta para verla más de cerca"
                DONE

> EXAMINAR MACETA AT lPorche
                CARRIED oMaceta ; Ha cogido la maceta
                ZERO fExaminarMaceta ; Activo un flag para controlar que YA examinó la maceta
                SET fExaminarMaceta
                MESSAGE "La maceta contiene aún arena y una planta que está más seca que la mojama."
                DONE

> EXAMINAR MACETA AT lPorche
                NOTZERO fExaminarMaceta 
                MESSAGE "Yo creo que ya está bien de jugar con la maceta."
                DONE

> EXAMINAR FELPUDO AT lPorche
	            MESSAGE "Es un clásico felpudo de esparto con el mensaje WELCOME HOME."
                DONE

> EXAMINAR GNOMO AT lPorche
                NOTCARR oGnomo ; No ha cogido el gnomo
                MESSAGE "Es un gnomo rechoncho, con sonrisa picaresca y una mirada intensa."
                DONE
                ;NOTCARR oGnomo
                ;ZERO fCogerGnomo
                ;SET fCogerGnomo
                ;ISAT oGnomo 252
                ;CREATE oGnomo
	            ;MESSAGE "Es un gnomo rechoncho, con sonrisa picaresca y una mirada intensa."
                ;DONE

> EXAMINAR GNOMO AT lPorche
                CARRIED oGnomo
                ;NOTZERO fCogerGnomo
	            MESSAGE "Lo miras con atención y lo agitas en el aire. Suena algo suelto dentro."
                DONE

> MIRAR     _ AT lPorche
                ISAT oMaceta 252
                CREATE oMaceta
                ISAT oFelpudo 252
                CREATE oFelpudo
                ISAT oGnomo 252
                CREATE oGnomo
                MESSAGE "A tu alrededor puedes ver en el suelo una vieja maceta con lo que debió ser una planta, un felpudo y un gnomo de jardín."
                DONE

> COGER FELPUDO AT lPorche
                ZERO fCogerFelpudo
                SET fCogerFelpudo
	            MESSAGE "Debajo del felpudo no hay nada a excepción de un cienpies que sale por patas al haber sido descubierto. Prefiero dejar el felpudo en su sitio."
                DONE

> COGER FELPUDO AT lPorche
                NOTZERO fCogerFelpudo
                NOTCARR oLlavePorche
	            MESSAGE "La llave de la casa no va a aparecer sola. Sigue buscando."
                DONE                

> TIRAR MACETA AT lPorche
                CARRIED oMaceta
	            MESSAGE "Tiras la maceta contra el suelo y no se rompe. De qué estará hecha?"
                DROP oMaceta
                DONE

> DEJAR MACETA AT lPorche
                CARRIED oMaceta
	            MESSAGE "Dejas la maceta en su sitio. Que buen hijo eres, si te viera tu madre estaría super orgullosa."
                DROP oMaceta
                DONE

> TIRAR GNOMO AT lPorche
                ISAT oLlavePorche 252
                CREATE oLlavePorche
                GET oLlavePorche
            	MESSAGE " al tirar el gnomo contra el suelo y saltar en mil pedazos. Es una llave metálica. Me encanta ese grupo! Ahora bien, cuando se entere tu madre que has roto el gnomo... vas a flipar!!!. "
                DESTROY oGnomo
                DONE	

> ESCUCHAR METALICA at lPorche
	            ;XPLAY "T110O5G16F#16G16A16G8O4G4B4O5D8D8C8C4C16O4B16O5C16D16C8O4D4A4O5C8C8"
                ;XPLAY "T110O4B8B4O5G4.E16D8C#8C#8C#8G4.E16D8C#8C#8C#8G4.E16F#8D8O4B8"
                ;XPLAY "T110O5G8E4.D32E32D4"
                MESSAGE "Igual no es el momento de escuchar esta música, pero..."
                DONE

> EXAMINAR PUERTA at lPorche
                MESSAGE "Es una puerta fuerte, hecha de madera maciza. Como todas las puertas, tiene una cerradura para introducir una llave. Ahora recuerdas que siempre quisiste que arreglaran el timbre en casa, porque cuando todo el mundo está en las habitaciones, nadie escuchaba nada."
                DONE

> GOLPEAR PUERTA at lPorche
                MESSAGE "Nadie responde a tu llamada. Prueba otra cosa."
                DONE

> USAR LLAVE
                SYNONYM ABRIR PUERTA

> ABRIR PUERTA	AT lPorche
                CARRIED oLlavePorche
            	MESSAGE "Ya estás dentro de la casa. Hace frío. Y apenas hay luz. Te has tropezado con la mesita de la entrada.#n- Hola? Hay alguien en casa?#n-No quiero bromitas porque sino me voy a cabrear y lo sabéis!#nNo responde nadie. La puerta se cierra tras de tí de un portazo. Ya no se abre. Estás encerrado."
                goto lDescansillo
                RESTART
                DONE

; Ya estamos dentro de la casa y no podemos salir al exterior

> MIRAR _       AT lDescansillo
                MESSAGE "Hay poca luz. Puedes ver las escaleras que suben a las habitaciones de la planta superior, el baño de la planta de abajo (N), la entrada a la cocina (S) y enfrente el salón (E). Y por supuesto, la mesita con la que has tropezado."
                DONE

> EXAMINAR MESITA AT lDescansillo
                ZERO fMesita
                SET fMesita
                MESSAGE "Es una preciosa mesita de madera con patas forjadas de hierro. Tu madre la heredó de sus padres. Tiene un cajón semiabierto."
                DONE

> EXAMINAR MESITA AT lDescansillo
                NOTZERO fMesita
                ZERO fCajon
                MESSAGE "La mesita sigue siendo preciosa. Abres el cajón y seguimos?"
                DONE

> EXAMINAR MESITA AT lDescansillo
                NOTZERO fMesita
                NOTZERO fCajon
                MESSAGE "La mesita sigue siendo preciosa. Seguimos?"
                DONE

> ABRIR CAJON   AT lDescansillo
                ISAT oLlave1 252
                ISAT oLlave2 252                
                ISAT oCerillas 252                
                MESSAGE "En el cajón de la mesita puedes ver las llaves del garage y de la leñera, además de unas cerillas. Lo coges todo!!!#n"   
                CREATE oLlave1
                GET oLlave1
                CREATE oLlave2
                GET oLlave2
                CREATE oCerillas
                GET oCerillas
                ZERO fCerillas
                SET fCerillas
                LET fCerillas 15
                ZERO fCajon
                SET fCajon
	            MESSAGE "Tienes una caja con 15 cerillas."
                DONE            

> ABRIR CAJON AT lDescansillo
                NOTZERO fCajon
                MESSAGE "Ya no hay nada interesante en la mesita."
                DONE

> EXAMINAR CERILLAS AT lDescansillo
                CARRIED oCerillas
                MESSAGE "Es una caja de cerillas de palo largo, típicas para encender los fogones de la cocina y velas. Contiene 15 cerillas."
                DONE

> ENCENDER CERILLA
                SYNONYM USAR CERILLA

> PRENDER CERILLA
                SYNONYM USAR CERILLA

> USAR CERILLA 
                CARRIED oCerillas
                NOTEQ fCerillas 0
                LET fLuz 2
                MINUS fCerillas 1
                MESSAGE "Las cerillas funcionan y ves bastante mejor. Pero ojo, se gastan.#nTe quedan:"
                PRINT fCerillas
                DONE

> BAJAR BODEGA AT lDescansillo
                MESSAGE "Necesitas la llave de la bodega para poder entrar."
                DONE

> S         _  AT lDescansillo ; No podemos entrar en la cocina sin LUZ
                ZERO fLuz
                SET fDark
                MESSAGE "La cocina está muy oscura. Sin luz no se puede entrar."
                DONE

> S         _  AT lDescansillo ; Tenemos LUZ y nos metemos en la cocina
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                GOTO lCocina
                RESTART
                DONE

> MIRAR     _ AT lCocina
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                MESSAGE "Puedes ver el fregadero, un mueble con cajones y la vieja lavadora que con 35 años, aún sigue dando servicio. Ya no se fabrican como antes."
                DONE

> EXAMINAR FREGADERO AT lCocina
                MESSAGE "No hay nada que ver en el fregadero. Está vacio."
                DONE

> EXAMINAR MUEBLE AT lCocina
                MESSAGE "Es un típico mueble de cocina con varios cajones."
                DONE

> EXAMINAR CAJONES AT lCocina
                MESSAGE "Puedes ver unas pilas, unas velas y las llaves del patio.#nQue curioso, no hay cuchillos."
                DONE

> EXAMINAR LAVADORA AT lCocina
                MESSAGE "Dentro encuentras un cuchillo grande de carnicero.#n¿Pero que puñetas hace eso ahí dentro?"
                DONE

> E         _  AT lCocina ; No podemos entrar en el almacén sin LUZ
                ZERO fLuz
                SET fDark
                MESSAGE "El almacén está muy oscura. Sin luz no se puede entrar."
                DONE

> E         _  AT lCocina ; Tenemos LUZ y nos metemos en el almacén
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                GOTO lAlmacen
                RESTART
                MESSAGE ""
                DONE

> ABRIR MUEBLE AT lAlmacen
                ISAT oLinterna 252
                CREATE oLinterna
                GET oLinterna
                DONE

> SUBIR     _  AT lDescansillo
                ZERO fLuz
                SET fDark
                ZERO fTablon
                NOTCARR oLinterna
                MESSAGE "Las escaleras están muy oscuras. Necesitas algo para iluminar."
                DONE

> SUBIR     _  AT lDescansillo
                NOTZERO fTablon                
                MESSAGE "Metes el pie accidentalmente en el escalón al que quitaste el tablón y ;tropiezas malamente. Caes rodando las escaleras y te partes el cuello. Has muerto."
                INKEY
                GOTO 0 ; vamos a la localización 0
                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc
                DONE

> SUBIR     _  AT lEscaleras
                ZERO fLuz
                SET fDark                
                NOTZERO fTablon                
                MESSAGE "Metes el pie accidentalmente en el escalón al que quitaste el tablón y ;tropiezas malamente. Caes rodando las escaleras y te partes el cuello. Has muerto."
                GOTO 0 ; vamos a la localización 0
                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc
                DONE

> SUBIR     _  AT lDescansillo
                ZERO fTablon
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                GOTO lEscaleras
                ;RESTART
                MESSAGE "#nNotas un pequeño crujido bajo tus pies. Siempre ha habido un tablón suelto en esa escalera y nadie se preocupó de arreglarlo."
                DONE

> SUBIR     _  AT lEscaleras
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> SUBIR     _  AT lEscaleras
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                ZERO fTablon
                MESSAGE "Menos mal que llevas algo de luz. Toda la planta superior está a oscuras."
                GOTO lPasillo1
                RESTART
                DONE

> EXAMINAR ESCALERAS AT lEscaleras
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                MESSAGE "Sientes curiosidad por examinar la escalera con más detalle."
                ISAT oTablon 252
                MESSAGE "Hay un tablón que parece tener cierta holgura y se mueve un poco."
                CREATE oTablon
                DONE

> EXAMINAR TABLON AT lEscaleras
                CLEAR fDark
                NOTZERO fLuz
                MESSAGE "Efectivamente está un poco suelto. Creo que podría moverlo."
                DONE

> EXAMINAR TABLON AT lEscaleras
                NOTZERO fTablon
                MESSAGE "El escalón está abierto, no has puesto el tablón en su sitio."
                DONE

> MOVER TABLON AT lEscaleras
                ZERO fTablon
                SET fTablon
                MESSAGE "Mueves el tablón y lo desplazas hacia un lado. Puedes ver una caja de metal dentro."
                DONE

> MOVER TABLON AT lEscaleras
                NOTZERO fTablon                
                MESSAGE "El tablón no se mueve más"                           

> EXAMINAR CAJA AT lEscaleras
                MESSAGE "La caja tiene un candado de seguridad. Necesitas una llave."
                DONE

> COGER CAJA AT lEscaleras
                MESSAGE "La caja está pegada dentro del hueco del escalón, no se puede coger."
                DONE

> ABRIR CAJA AT lEscaleras
                NOTCARR oLlaveCaja
                MESSAGE "No tienes la llave. No puedes abrirla."
                DONE

> ABRIR CAJA AT lEscaleras
                CARRIED oLlaveCaja
                ISAT oSecretKey1 252
                CREATE oSecretKey1
                MESSAGE "Has conseguido abrir la caja." 
                GET oSecretKey1
                MESSAGE ""¿Que abrirá esa tarjeta de seguridad?""
                DONE

> PONER TABLON AT lEscaleras
                NOTZERO fTablon
                CLEAR fTablon
                MESSAGE "Has puesto el tablón en su sitio."
                DONE                

> PONER TABLON AT lEscaleras
                ZERO fTablon
                MESSAGE "Has puesto el tablón en su sitio."
                DONE      

> BAJAR     _  AT lEscaleras
                CLEAR fDark
                ZERO oTablon
                GOTO lDescansillo
                MESSAGE "Has vuelto al descansillo."
                DONE

;> BAJAR     _  AT lPasillo1
;                ZERO fLuz
;                SET fDark                
;                NOTZERO fTablon                
;                MESSAGE "Metes el pie accidentalmente en el escalón al que quitaste el tablón y tropiezas malamente. Caes rodando las escaleras y te partes el cuello. Has muerto."
;                GOTO 0 ; vamos a la localización 0
;                RESTART ; Y recargamos todo, gráficos, descripcion de la LOCalizacion, etc
;                DONE

> E     _      AT lPasillo1 
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> E     _      AT lPasillo1
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                GOTO lPasillo2
                RESTART
                DONE

> BAJAR     _  AT lPasillo1 
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> BAJAR     _  AT lPasillo1
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                GOTO lEscaleras
                RESTART
                DONE

> E     _      AT lPasillo2
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> E     _      AT lPasillo2
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo3
                RESTART
                DONE

> O     _      AT lPasillo2
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> O     _      AT lPasillo2
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo1
                RESTART
                DONE

> O     _      AT lPasillo3
                ZERO fLuz
                SET fDark
                MESSAGE "No puedes moverte si vas a ciegas. Utiliza algo para iluminarte."
                DONE

> O     _      AT lPasillo3
                CLEAR fDark
                NOTZERO fLuz
                NOTZERO fCerillas
                CARRIED oCerillas
                MESSAGE "Con algo que genere luz, siempre será mucho mejor desplazarse por esta planta."
                GOTO lPasillo2
                RESTART
                DONE

> E     _      AT lPorche
                Zero fPuertaPorche
                MESSAGE "No puedes entrar la casa. La puerta está cerrada y necesitas la llave. Pero recuerdas que cuando tus padres salían a pasear por el bosque, solían esconder una llave en algún sitio. Lo recuerdas?"
                DONE

> E     _      AT lPorche 
                CARRIED oLlavePorche
            	MESSAGE "Ya estás dentro de la casa. Hace frío. Y apenas hay luz. Te has tropezado con la mesita de la entrada.#n- Hola? Hay alguien en casa?#n-No quiero bromitas porque sino me voy a cabrear y lo sabéis!#nNo responde nadie."
                goto lDescansillo
                DONE

> E     _      AT lMadrid ; Si no has usado el movil y quieres salir al Este
                Zero fUsarMovil
                MESSAGE "No crees que deberías atender al móvil?"
                DONE

> E     _      AT lMadrid ; Si no has usado el movil y quieres salir al Este
                NOTZERO fUsarMovil
                CARRIED oMovil
                MESSAGE "Muy bien! Te has dejado el móvil en casa. No puedes volver porque vas con prisas."
                DESTROY oMovil
                INKEY
                CLS
                GOTO lCoche
                RESTART
                DONE

> E     _     AT lCoche
              CLS
              GOTO lExtcasa
              RESTART
              DONE

> N     _      AT lExtcasa
                Zero fPuertaLenera
                MESSAGE "No puedes entrar en la leñera. La puerta está cerrada. Necesitas una llave."
                DONE

> S     _      AT lExtcasa
                Zero fPuertaGarage
                MESSAGE "No puedes entrar en el garage. La puerta está cerrada.Necesitas una llave."
                DONE



> I       _     SYSMESS 9
                LISTAT  CARRIED
                SYSMESS 10
                LISTAT  WORN
                DONE

> COGER   TODO  DOALL   HERE                
> COGER   _     AUTOG
                DONE

> DEJAR   TODO  DOALL   CARRIED
> DEJAR   _     AUTOD
                DONE

> QUITAR  TODO  DOALL   WORN
> QUITAR  _     AUTOR
                DONE

> PONER   TODO  DOALL   CARRIED
> PONER   _     AUTOW
                DONE

> R       _     RESTART

> FIN     _     QUIT
                END

> FIN     _     DONE                    

> SV      _     SAVE 0                  
                RESTART

> LD      _     LOAD 0
                RESTART

> RAMSA   _     RAMSAVE
                RESTART

> RAMLO   _     RAMLOAD 255                             ;Reload all flags
                RESTART

> MIRAR   _     RESTART

/PRO 6 ; Inicialización

> _       _     WINDOW  0               ; Window para imágenes
                WINAT   0       0
                WINSIZE 14      127     ; 14 filas de alto * 8 pixels por fila = 112 pixels
                PICTURE 0
                DISPLAY 0

> _       _     WINDOW  1               ; Window para los textos
                WINAT   14      0
                WINSIZE 25      127     ; Tamaño máximo
                CLS
                DESC    0               ; Introducción
                ANYKEY
                CLS
                SET     255             ; Pone a cero todas las banderas

> _       _     MINUS   255     1
                NOTEQ   255     fGFlags ; menos GFlags!!!
                CLEAR   @255

> _       _     NOTZERO 255
                SKIP    -2

> _       _     RESET                   ; Objetos a su loc. inicial / Flag 1
                LET     fStrength 10
                LET     fMaxCarr  4
                SET     fCPronounNoun
                SET     fCPronounAdject
                GOTO    1               ; Primera localidad del juego
/END
